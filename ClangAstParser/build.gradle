plugins {
    id 'distribution'
    id 'java'
    id 'jacoco'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()
}

dependencies {
    implementation ":ClavaAst"
    implementation ":CommonsLangPlus"
    implementation ":jOptions"
    implementation ":SpecsUtils"

    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.10.0'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test-resources'
        }
    }
}

// Test coverage configuration
jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70 // 70% minimum coverage
            }
        }
    }
}

// Make sure jacoco report is generated after tests
test {
    useJUnitPlatform()

    maxParallelForks = Runtime.runtime.availableProcessors()

    finalizedBy jacocoTestReport
}
