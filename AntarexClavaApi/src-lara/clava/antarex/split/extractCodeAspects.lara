/*
   Author(s): Lo√Øc Besnard (loic.besnard@irisa.fr). 
              IRISA-INRIA Rennes, France.
              
   This code has been developed during the ANTAREX project (http://www.antarex-project.eu).
   It is provided under the MIT license (https://opensource.org/licenses/MIT).
*/
/* This package defines, for C, C++ languages, the aspects 
    - that (1) extract fragment of code into a new function or method (C, C++ languages), in separated file.
    - that (2) isolate function/method in a new file.
    
 * To apply (1) the statement to extract must be annotated by the pragma named  EXTRACT
 * To apply (2) the function/method to isolate must be annotated by the pragma named ISOLATE NB
    where NB is the number of copies of the function/method, but with a new name, will be 
    generated in separated files. The original function/method is then transformed as a selector of version.
 * Example of a session:
    // initialize
     call extractIsolate_initialize();
     
     call extractCode();  // for application of (1)
     call isolateFunctionsMethods(); // for application of (2)
     
     // Finalyze
     call extractIsolate_finalize();   
    end
  * */
  
import antarex.utils.messages;
import antarex.utils.mangling;
import antarex.utils.lowLevelFuncs;
import antarex.utils.sysfile;
import antarex.split.splitDeclarations;
import antarex.split.extractCodeFuncs;
// var modifiedFiles=[];

/**
 Extract the code annotated with the prama called "EXTRACT".
 The selected code becomes the body of a new function/method generated in a new file.
 Then the user may play with several compiled versions of this code, or modified algorithm.
*/
aspectdef extractCode
 var $externDecls;
 var found=false;
 resetModifiedFiles();
 
 // First step.
 select pragma end
  apply
    if (isBeginIsolateCodePragma($pragma)) {
      var $refFuncs = [ ];
      found = true;
      var $target = $pragma.target;
      mkNewExtractedCodeIdentifier();
      $externDecls = getExternalDeclsOf($target, $refFuncs);
      substituteSymbols($externDecls);
      declNewSymbolsAsGlobals($externDecls);
      keepInitDeclarations($externDecls);
      cleanCode($externDecls);
      $target.insert before "#pragma " + $TMP_PRAGMA +" " + getCurrentExtractedCodeIdentifier();
      addModifiedFiles($target);
    }
 end
 if (!found) {
  MESSAGE("No pragma ", $EXTRACT_CODE_PRAGMA, " found !!!");
  return;
 }
  //rebuildFiles(getModifiedFiles());
 Clava.rebuild();
 // Second step.
 select pragma end
  apply
    if (isBeginIsolateCodePragma($pragma)) {
      var $target = $pragma.target;
      var $name = getAssociatedName($target);
      declareNewIsolateCodeFunctionOrMethod($target, $name);
      var $params = getEffectiveParameters($target);
      replaceCodeByCall($target, $name, $params);
	  $pragma.detach();
    }
    end
    // third step, cleaning: the temporary pragma is removed.
  select pragma end
  apply
    if ($pragma.name === $TMP_PRAGMA) $pragma.detach();
  end
end

/**
  Initialize the components of the code extraction.
*/
aspectdef extractIsolate_initialize

end

/**
  Finalize the components of the code extraction.
*/
aspectdef extractIsolate_finalize

end

/**
  For a function/method annotated with the pragma 
      #pragma ISOLATE NB  
  It produces NB versions of the function/method, copy of the original but with a new name.
  The original one becomes the selector of the version to use.
*/
aspectdef isolateFunctionsMethods
 var $externDecls;

 select pragma end
  apply
    if (isBeginIsolatePragma($pragma)) {
      var $target = $pragma.target;
      var ok =  isaMethod($target) || isaFunction($target);
      
      if (!ok) 
      	ERROR( " *** Unwaited pragma for: " + $target.code + " ( not a function or method !!!)");
      else {
        if (! $target.hasDefinition)
      	  ERROR("pragma ISOLATE, assigned to " + $target.code + " must be applied on function or method definition, not on declaration");
        else {
          if (isaFunction($target)) {
          	if ($target.storageClass === 'static') {
          		  WARNING(" the function " + $target.name + " becomes external");
          		  $target.setValue('storageClass', 'NONE');
          		}
          }
          var $body=$target.body;
          var nbversions = $pragma.content;
          if (nbversions === "") nbversions= "1";
          var n = parseInt(nbversions);
          if ( n < 0) 
            ERROR("Bad value for the " + $ISOLATE_PRAGMA + " pragma");
          else {
            var tabNewFuncs = genIsolateCodeVersions($target, n);
            genSelectorCode($target, tabNewFuncs);
            genDeclCreatedObjects($target, tabNewFuncs);
            $pragma.detach();
          }
        }
      }
    }
 end
  
end


