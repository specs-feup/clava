plugins {
    id 'distribution'
    id 'application' // Executable
    id 'java' // Java project
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

application {
    mainClass.set("pt.up.fe.specs.clava.weaver.ClavaWeaverLauncher")

    // For most input codes this is not necessary, but for the largest example we have tested (LSU-gcc, ~700.000 LoC)
    // it needs a large amount of memory
    //applicationDefaultJvmArgs = ["-Xmx6g"]
}

// Repositories providers
repositories {
    // Gearman
    maven { url="https://oss.sonatype.org/content/repositories/snapshots" }

    mavenCentral()
}

dependencies {
    implementation ":CommonsLangPlus"
    implementation ":GsonPlus"
    implementation ":jOptions"
    implementation ":JsEngine"
    implementation ":SpecsUtils"
    implementation ":XStreamPlus"
    implementation ":tdrcLibrary"

    implementation ":LanguageSpecification"
    implementation ":LaraFramework"
    implementation ":LARAI"
    implementation ":LaraUtils"
    implementation ":WeaverGenerator"
    implementation ":WeaverInterface"

    implementation ":AntarexClavaApi"
    implementation ":ClangAstParser"
    implementation ":ClavaAst"
    implementation ":ClavaHls"
    implementation ":ClavaLaraApi"


    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.12.1'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

// Clava
task clava(type: JavaExec) {
    group = "Execution"
    description = "Launches Clava"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'pt.up.fe.specs.clava.weaver.ClavaWeaverLauncher'
    args = []
}

// Re-run the weaver generator
task generateWeaver(type: Exec) {
    group = "Execution"
    description = "Generates the Weaver Abstracts"

    executable "./../../lara-framework/WeaverGenerator/build/install/WeaverGenerator/bin/WeaverGenerator"

    args = [
        "-w", "CxxWeaver",
        "-x", "./resources/clava/weaverspecs",
        "-o", "./src",
        "-p", "pt.up.fe.specs.clava.weaver",
        "-n", "pt.up.fe.specs.clava.ClavaNode",
        "-j",
        "-d",
    ]
}
