plugins {
    id 'distribution'
    id 'application'
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}


// Repositories providers
repositories {
    // Gearman
    maven { url="https://oss.sonatype.org/content/repositories/snapshots" }

    mavenCentral()
}

configurations {
    weaverGeneratorRuntime
}

dependencies {
    implementation ":jOptions"
    implementation ":SpecsUtils"

    implementation ":LanguageSpecification"
    implementation ":LARAI"
    implementation ":LaraUtils"
    implementation ":WeaverInterface"

    implementation ":AntarexClavaApi"
    implementation ":ClangAstParser"
    implementation ":ClavaAst"
    implementation ":ClavaLaraApi"

    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'

    weaverGeneratorRuntime ":WeaverGenerator"
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

// Re-run the weaver generator
task generateWeaver(type: JavaExec) {
    group = "Execution"
    description = "Generates the Weaver Abstracts"
    classpath = configurations.weaverGeneratorRuntime
    mainClass = 'org.lara.interpreter.weaver.generator.commandline.WeaverGenerator'
    args = [
        "-w", "CxxWeaver",
        "-x", "./resources/clava/weaverspecs",
        "-o", "./src",
        "-p", "pt.up.fe.specs.clava.weaver",
        "-n", "pt.up.fe.specs.clava.ClavaNode",
        "-e",
        "-j"
    ]
}

compileJava.dependsOn generateWeaver
