plugins {
    id 'distribution'
    id 'application' // Executable
    id 'java' // Java project
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

application {
    mainClass.set("pt.up.fe.specs.clava.weaver.ClavaWeaverLauncher")

    // For most input codes this is not necessary, but for the largest example we have tested (LSU-gcc, ~700.000 LoC)
    // it needs a large amount of memory
    //applicationDefaultJvmArgs = ["-Xmx6g"]
}

// Repositories providers
repositories {
    // Gearman
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

    mavenCentral()
}

dependencies {
    implementation "junit:junit:4.13.1"

    implementation ":CommonsLangPlus"
    implementation ":GsonPlus"
    implementation ":jOptions"
    implementation ":JsEngine"
    implementation ":SpecsUtils"
    implementation ":XStreamPlus"
    implementation ":tdrcLibrary"

    implementation ":LanguageSpecification"
    implementation ":LaraFramework"
    implementation ":LARAI"
    implementation ":LaraUtils"
    implementation ":WeaverGenerator"
    implementation ":WeaverInterface"

    implementation ":AntarexClavaApi"
    implementation ":ClangAstParser"
    implementation ":ClavaAst"
    implementation ":ClavaHls"
    implementation ":ClavaLaraApi"


    implementation group: 'com.google.guava', name: 'guava', version: '19.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.4'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }

        resources {
            srcDir 'resources'
        }
    }


    test {
        resources {
            srcDir 'resources'
        }
    }
}

// Clava
task clava(type: JavaExec) {
    group = "Execution"
    description = "Launches Clava"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'pt.up.fe.specs.clava.weaver.ClavaWeaverLauncher'
    args = []
}

// Clava Doc (old version)
task clavadoc(type: JavaExec) {
    group = "Execution"
    description = "Generates Documentation (Legacy)"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'pt.up.fe.specs.clava.weaver.ClavaWeaverLauncher'
    args = [
            '-doc',
            '--output',
            '"../docs/api"',
            '--clean',
            '--exclude',
            '_',
            '--packages',
            '{\'Clava API\': [\'../ClavaLaraApi/src-lara-clava/clava\'], \'LARA API\': [\'../LaraApi/src-lara-base\',\'../LaraApi}/src-lara\',\'../LARAI/src-lara\',\'../LaraExtraApi/src-lara\',\'../ClavaLaraApi/src-lara/clava\'], \'ANTAREX API\': [\'../AntarexClavaApi/src-lara/clava\']}',
    ]
}